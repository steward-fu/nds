precision highp float;

uniform float frag_alpha;
uniform vec4 frag_screen;
varying vec2 frag_tex_coord;
uniform sampler2D frag_tex_sample;

#define PI 3.141592654

#define BRIGHTEN_SCANLINES 16.0
#define BRIGHTEN_LCD 4.0

#define PSP_SCREEN_HEIGHT 272.0

#define TARGET_GAMMA 2.21
#define CC_R 0.98
#define CC_G 0.795
#define CC_B 0.98
#define CC_RG 0.04
#define CC_RB 0.01
#define CC_GR 0.20
#define CC_GB 0.01
#define CC_BR -0.18
#define CC_BG 0.165

void main()
{
    const float INV_DISPLAY_GAMMA = 1.0 / 2.2;

    vec2 pixelCoord = frag_tex_coord * frag_screen.xy;
    vec2 angle = 2.0 * PI * ((pixelCoord * PSP_SCREEN_HEIGHT * frag_screen.w) - 0.25);

    float yfactor = (BRIGHTEN_SCANLINES + sin(angle.y)) / (BRIGHTEN_SCANLINES + 1.0);
    float xfactor = (BRIGHTEN_LCD + sin(angle.x)) / (BRIGHTEN_LCD + 1.0);

    vec3 colour = texture2D(frag_tex_sample, frag_tex_coord).bgr;

    colour.rgb = pow(colour.rgb, vec3(TARGET_GAMMA));
    colour.rgb = mat3(CC_R, CC_RG, CC_RB,
        CC_GR, CC_G, CC_GB,
        CC_BR, CC_BG, CC_B) * colour.rgb;
    colour.rgb = clamp(pow(colour.rgb, vec3(INV_DISPLAY_GAMMA)), 0.0, 1.0);

    colour.rgb = yfactor * xfactor * colour.rgb;

    gl_FragColor = vec4(colour.rgb, frag_alpha);
}
